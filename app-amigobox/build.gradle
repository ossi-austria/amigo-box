import org.ossiaustria.dependencies.AndroidVersion
import org.ossiaustria.dependencies.Libs
import org.ossiaustria.dependencies.Versions

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'de.mannodermaus.android-junit5'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id "com.github.ben-manes.versions"
}

android {

    compileSdkVersion AndroidVersion.compileSdk

    defaultConfig {
        applicationId "org.ossiaustria.amigobox"
        minSdkVersion AndroidVersion.minSdk
        targetSdkVersion AndroidVersion.targetSdk
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            testCoverageEnabled true
            buildConfigField "String", "API_ENDPOINT", "\"API_ENDPOINT\""
            aaptOptions.cruncherEnabled = false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    buildFeatures {
        viewBinding true
        compose true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        reportDir "${rootProject.buildDir}/test-reports"
        resultsDir "${rootProject.buildDir}/test-results"
        junitPlatform { instrumentationTests.integrityCheckEnabled = true }
        unitTests { includeAndroidResources = true }
    }
    composeOptions {
        kotlinCompilerExtensionVersion Versions.compose_version
    }
    kapt { correctErrorTypes true }
    kotlinOptions { jvmTarget = '1.8' }
    lintOptions {
        disable "Instantiatable"
    }
}


dependencies {
    implementation project(":lib-commons")
    implementation project(":lib-domain")
    implementation project(":lib-nfc")
    implementation project(":lib-jitsi")

    implementation(Libs.timber)
    implementation(Libs.appcompat)
    implementation(Libs.material)

    implementation(Libs.Koin.koinCore)
    implementation(Libs.Koin.koinAndroid)
    testImplementation(Libs.Koin.koinTest)
    testImplementation(Libs.Koin.koinTestJunit)

    implementation(Libs.Compose.ui)
    implementation(Libs.Compose.uiTooling)
    implementation(Libs.Compose.activity)
    implementation(Libs.Compose.viewmodel)
    implementation(Libs.Compose.livedata)
    implementation(Libs.Compose.foundation)
    implementation(Libs.Compose.animation)
    implementation(Libs.Compose.material)
    implementation(Libs.Compose.materialIconsCore)
//    implementation(Libs.Compose.materialIconsExtended)
    implementation(Libs.Coil.coil)
    implementation(Libs.Coil.coilCompose)

    implementation(Libs.coroutinesAndroid)
    implementation(Libs.coroutinesCore)
    implementation(Libs.Lifecycle.livedata)
    implementation(Libs.Lifecycle.livedataKtx)
    implementation(Libs.Lifecycle.lifecycleKtx)

    implementation(Libs.navigationRuntimeKtx)
    implementation(Libs.navigationFragmentKtx)
    implementation(Libs.navigationUiKtx)
    implementation(Libs.navigationDynamicFeatures)
    androidTestImplementation(Libs.navigationTest)

    implementation platform(Libs.Firebase.firebaseBom)
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging'

    implementation Libs.permissions

    // Testing
    testImplementation project(':lib-test-commons')
    testImplementation(Libs.Test.jupiterApi)
    testImplementation(Libs.Test.jupiterParams)
//    testRuntimeOnly(Libs.Test.jupiterEngine) //Junit5 still broken
    testRuntimeOnly(Libs.Test.jupiterVintageEngine)

    androidTestImplementation(Libs.Test.testRunner)
    androidTestImplementation(Libs.Test.jupiterApi)
    androidTestImplementation(Libs.Test.mannodermausAndroidTestCore)
    androidTestRuntimeOnly(Libs.Test.mannodermausAndroidTestRunner)

    implementation(Libs.Test.coroutinesTest)
    testImplementation(Libs.Test.test)
    testImplementation(Libs.Test.testExt)
    testImplementation(Libs.Test.robolectric)
    testImplementation(Libs.Test.coroutinesTest)
    testImplementation(Libs.Lifecycle.coreTesting)
    testImplementation(Libs.Test.mockk)

    androidTestImplementation(Libs.Test.mockkAndroid)

    androidTestImplementation(Libs.Test.espressoCore)
    androidTestImplementation(Libs.Test.espressoContrib)
    androidTestImplementation(Libs.Test.barista)
}
